@@ Territories code (#2279)

@create Territories Code
@link Territories Code = #2
@set Territories Code = !NO_COMMAND
@power Territories Code=SQL_OK

@lock Territories Code=FLAG^WIZARD|FLAG^ROYALTY|DBREFLIST^ALLOW
@lock/command Territories Code=FLAG^WIZARD|FLAG^ROYALTY|DBREFLIST^ALLOW

&LIST`TERRITORIES_1_0 %wt=Kindred|Animal|Blood|Mysterious|Underworld|Gang|Government|Police|Neighborhood|Monster
&LIST`TERRITORIES_1_1 %wt=Lunatic|Kindred|Street|Police|Gang|Homeless|Activist|Unstable|Reporter|Outsider
&LIST`TERRITORIES_1_2 %wt=Kindred|Bureaucratic|Church|Financial|Street|Health|High Society|Industry|Legal|Occult
&LIST`TERRITORIES_1_3 %wt=Kindred|Media|Occult|Police|Street|Political|Transportation|Underworld|Education|Monster
&LIST`TERRITORIES_1_4 %wt=Ordo Dracul|Lancea Et Sanctum|Invictus|Carthian|Daeva|Mekhet|Gangrel|Ventrue|Nosferatu
&LIST`TERRITORIES_2_0 %wt=Interest|Rampage|Infection|Tradition|Hideout|Buyout|Violence|Observation|Collapse|Seizure
&LIST`TERRITORIES_2_1 %wt=Accident|Scandal|Murder|Suicide|Corruption|Theft|Exposure|Blackmail|Violence|Alliance
&LIST`TERRITORIES_3_0 %wt=Arson|Weapons|Conflict|None|None|None|None|Magic|Children|Possession
&LIST`TERRITORIES_3_1 %wt=None|None|None|None|None|Money|Covenant|God Machine|None|None

@@ General commands
&CMD`TERRITORIES %wt=$+territories:[pemit(%#,[u(%wt/FN`TERRITORIES)])]
&CMD`TERRITORIES_KINGPIN %wt=$+territories/kp *=*:[SETQ(TERR,[u(%vt/FN`VALID_TERRITORY,%0)])][SETQ(PC, [secure( [pmatch(%1)] )] )];@assert %Q<PC>=@nspemit/silent %#=+territories: No such player by that name; @assert %Q<TERR>=@nspemit/silent %#=+territories: No such territory by that name;@break [u(%wt/FN`TERRITORIES.SET_KINGPIN,%Q<TERR>,%Q<PC>)]=@nspemit/silent %#=+territories: Error setting Kingpin;[pemit(%#,+territories: Kingpin for %0 set to [name(%Q<PC>)])]

&CMD`TERRITORIES_STAT_ATTITUDE %wt=$+territories/att *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,attitude,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Attitude for %0 set to %q<STAT>)])]
&CMD`TERRITORIES_STAT_ACCESS %wt=$+territories/acc *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,access,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Access for %0 set to %q<STAT>)])]
&CMD`TERRITORIES_STAT_SAFETY %wt=$+territories/saf *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,safety,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Safety for %0 set to %q<STAT>)])]
&CMD`TERRITORIES_STAT_INFORMATION %wt=$+territories/inf *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,information,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Information for %0 set to %q<STAT>)])]
&CMD`TERRITORIES_STAT_IGNORANCE %wt=$+territories/ign *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,ignorance,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Ignorance for %0 set to %q<STAT>)])]
&CMD`TERRITORIES_STAT_PRESTIGE %wt=$+territories/pre *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,prestige,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Prestige for %0 set to %q<STAT>)])]
&CMD`TERRITORIES_STAT_STABILITY %wt=$+territories/sta *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,stability,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Stability for %0 set to %q<STAT>)])]
&CMD`TERRITORIES_STAT_FEEDING %wt=$+territories/fp *=*:[IF([u(%wt/FN`TERRITORIES_STAT_CHECK,%0,%1)],[IF([u(FN`TERRITORIES.SET_STAT,%Q<TERR>,feeding_pool,%Q<STAT>)],[pemit(%#,+territories:Error setting stat)],[pemit(%#,+territories:Feeding Pool for %0 set to %q<STAT>)])]

&CMD`TERRITORIES_KINGPIN_CLEAR %wt=$+territories/kp/clear *:[SETQ(TERR,[u(%vt/FN`VALID_TERRITORY,%0)])] @assert %Q<TERR>=@nspemit/silent %#=+territories: No such territory by that name;@break [u(%wt/FN`TERRITORIES.SET_KINGPIN,%Q<TERR>,NULL)]=@nspemit/silent %#=+territories: Error setting Kingpin;[pemit(%#,Kingpin for %0 cleared.)]

@@ Territory Actions

&CMD`TERRITORIES_REPORT %wt=$+territories/report *:@assert [u(%vt/FN`VALID_TERRITORY,%0)]=@nspemit/silent %#=+territories: No such territory by that name;[pemit(%#,[header(Territory Report for [ucstr(%0)])])]%r[pemit(%#,Active crises: [u(%vt/FN`TERRITORIES.ACTIVE_CRISES,%0)]%r)][pemit(%#,Current Actions)][pemit(%#,[u(%wt/FN`TERRITORIES.SELECT_ACTIONS,%0)][pemit(%#,%rRecent Actions)][pemit(%#,[u(%wt/FN`TERRITORIES.SELECT_ACTIONS,%0,-1)])][pemit(%#,Feeding Report)][pemit(%#,[u(%wh/FN`MONTHLY_BP_REPORT_BY_TERRITORY,%0)])][iter(lsearch(all,type,player),[switch([get(%i0/CHARGEN`APPROVED)][t([match(xget(%i0,PC_DATA`MERITS),Safe Place %0*,|)])],11,[setq(LIST,[strcat(%q<LIST>,name(%i0),[space(1)])])])])][pemit(%#,%rHavens here: %q<LIST>)]

&CMD`TERRITORIES_ACTIONS %wt=$+territories/actions *:@assert [u(%vt/FN`VALID_TERRITORY,%0)]=@nspemit/silent %#=+territories: No such territory by that name;[pemit(%#,[pemit(%#,Actions this week)][u(%wt/FN`TERRITORIES.SELECT_ACTIONS,%0)])]


&CMD`TERRITORIES_PATROL %wt=$^\+territories/patrol (\w+)=([\w\s]+)\|?(\d*)?$:[setq(EX_DICE,[IF(%3,%3,0)])][if([u(FN`TERRITORIES_ACTION.CHECK,%1,%2,%#)],[IF([u(%vt/FN`TERRITORIES.ACTION.PATROL_RUN,%q<PC>,%q<T>,%2,%Q<EX_DICE>,1)],[pemit(%#,+territories: [name(%Q<PC>) patrols %2 for 7 hours with %Q<SUCCS> successes])],[pemit(%#,+territories: Patrol failed \(Probably due to downtime\))])])]
@set %wt/CMD`TERRITORIES_PATROL=regexp

&CMD`TERRITORIES_RETAINER_PATROL %wt=$^\+territories/retainerpatrol ([\w\s]+)=(\w+)\|?(\d*)?$:[setq(EX_DICE,[IF(%3,%3,0)])][if([u(FN`TERRITORIES_ACTION_RETAINER.CHECK,%1,%2,%#)],[IF([u(%vt/FN`TERRITORIES.ACTION.PATROL_RUN,%q<PC>,%q<T>,%2,%Q<EX_DICE>,0)],[pemit(%#,+territories: [name(%Q<PC>) patrols %2 for 7 hours with %Q<SUCCS> successes])],[pemit(%#,+territories: Patrol failed \(Probably due to downtime\))])])]
@set %wt/CMD`TERRITORIES_RETAINER_PATROL=regexp

@@ Player=Territory|Succs|DT
&CMD`TERRITORIES_INVESTIGATE %wt=$+territories/investigate *=*|*|*:[if([u(FN`TERRITORIES_ACTION.CHECK,%0,%1,%#)],[IF([u(%vt/FN`TERRITORIES.ACTION.INVESTIGATE_RUN,%q<PC>,%q<T>,%1,%2,%3)][pemit(%#,+territories: [name(%Q<PC>) investigates %1 for %3 hours with %2 successes])],[pemit(%#,+territories: Investigate failed \(Probably due to downtime\))])])]

&CMD`TERRITORIES_FINESSE %wt=$+territories/finesse *=*|*|*:[if([u(FN`TERRITORIES_ACTION.CHECK,%0,%1,%#)],[u(%vt/FN`TERRITORIES.ACTION.FINESSE_RUN,%q<PC>,%q<T>,%1,%2,%3)][pemit(%#,+territories: [name(%Q<PC>) finesses %1 for %3 hours with %2 successes])])]

&CMD`TERRITORIES_SITE %wt=$+territories/site *=*:[setq(A,[sql(select territory_dbref from territory_site where id=[sqlescape(%1)])])][setq(B,[sql(select name from territory where dbref=%qA)])];@assert [t(%qA)]=@pemit %#=Couldn't find a territory for Site ID %1 or couldn't match player %0. Double check.;@switch [t([u(FN`TERRITORIES_ACTION.CHECK,%0,%qB,%#)])]=1,{[u(%vt/FN`TERRITORIES.ACTION.SITE_RUN,%q<PC>,%q<T>,%qB)][pemit(%#,+territories: [name(%Q<PC>) uses a site in %qB])];@trig #3745/TRIG`NOTIFY_SITE_USAGE=%q<PC>,[secs()],%qA,[sql(select name from territory_site where id=[sqlescape(%1)])],[sql(select open from territory_site where id=[sqlescape(%1)])]}

@@ Crises commands
&CMD`TERRITORIES_CG %wt=$+territories/cg:[pemit(%#,[u(%wt/FN`TERRITORIES_CG)])]
&CMD`TERRITORIES_CRISES %wt=$+territories/crises *:@assert [u(%vt/FN`VALID_TERRITORY,%0)]=@nspemit/silent %#=+territories: No such territory by that name;[pemit(%#,[u(%wt/FN`TERRITORIES.SELECT_CRISES,%0)])]
&CMD`TERRITORIES_CRISES_TBD %wt=$+territories/crisestbd:[u(%wt/FN`TERRITORIES.SELECT_TBD_CRISES)]

@@ Territory Crisis create: Territory Name|Crisis Name|Crisis Description|Rating|Active Date
&CMD`TERRITORIES_CC %wt=$+territories/cc *|*|*|*|*|*:[if([u(FN`TERRITORIES_CC.CHECK,%0,%1,%2,%3,%4,%5)],[u(FN`TERRITORIES_CC.RUN,%q<T>,%1,%2,%3,%4,%5)])]
&CMD`TERRITORIES_CD %wt=$+territories/cd *:[sql([u(%vt/FN`TERRITORIES.DELETE_CRISIS_SQL,%0)])][pemit(%#,Crisis deleted.)]
&CMD`TERRITORIES_CU %wt=$+territories/cu *=*|*|*|*:[if([u(FN`TERRITORIES_CU.CHECK,%0,%1,%2,%3,%4)],[u(FN`TERRITORIES_CU.RUN,%0,%1,%2,%3,%4)])]
&CMD`TERRITORIES_CS %wt=$+territories/cs *=*:[if([u(FN`TERRITORIES_CS.CHECK,%0,%1)],[u(%vt/FN`TERRITORIES.ADD_ACTION,[sql([u(%vt/FN`CRISIS_TERRITORY_SQL,%0)])],[u(%vt/FN`TERRITORIES.ACTION.NAME_TO_TYPE,Solve crisis)],[trim(num(%Q<PC>),#)],[sql([u(%vt/FN`CRISIS_RATING_SQL,%0)])],[sql_date_time()])][sql([u(%vt/FN`TERRITORIES.SOLVE_CRISIS_SQL,%0,[trim(num(%Q<PC>),#)])])][pemit(%#,Crisis %0 marked as solved by [name(%Q<PC>)].)])]

@@ Admin functions
@@ %0=Player Name, %1=Territory
&FN`TERRITORIES_ACTION.CHECK %wt=[setq(RET,1)][SETQ(PC, [secure( [pmatch(%0)] )] )][if(not(%q<PC>),[setq(RET,0)][pemit(%2,+territories: No such player by that name.)])][setq(T,[u(%vt/FN`VALID_TERRITORY,%1)])][if(not(%q<T>),[setq(RET,0)][pemit(%2,+territories: No such territory by that name.)])]%q<RET>

&FN`TERRITORIES_ACTION_RETAINER.CHECK %wt=[setq(RET,1)][SETQ(PC, [u(%wd/FN`RETAINER_FIND,%0)] )][if(not(%q<PC>),[setq(RET,0)][pemit(%2,+territories: No such Retainer by that name.)])][setq(T,[u(%vt/FN`VALID_TERRITORY,%1)])][if(not(%q<T>),[setq(RET,0)][pemit(%2,+territories: No such territory by that name.)])]%q<RET>

&FN`TERRITORIES %wt=[space(1)][align(31 15,Name-ID,Kingpin)][lalign(>3 >3 >3 >3 >3 >3 >3 >3,At|Ac|Sa|In|Ig|Pr|St|FP,|)]%r[space(1)][mapsql(FN`TERRITORIES_ROW,[u(%vt/FN`TERRITORIES.SELECT_SQL)])]
&FN`TERRITORIES_ROW %wt=[align(31 15,%1,[if(%4,[name(#%4)],None)])[lalign(>3 >3 >3 >3 >3 >3 >3 >3,%3,|)]%r

&FN`TERRITORIES_STAT_CHECK %wt=[setq(T,1)][SETQ(TERR,[u(%vt/FN`VALID_TERRITORY,%0)])][IF(not(%Q<TERR>),[setq(T,0)][pemit(%#,+territories: No such territory by that name)])][SETQ(STAT,%1)[IF([not(and([isnum(%Q<STAT>)],[gte(%Q<STAT>,-5)],[lte(%Q<STAT>,5)]))],[setq(T,0)][pemit(%#,+territories: Stat must be between -5 and 5)])]%q<T>

@@ Crises

@@ Check and set Valid Territory, Check valid name, Check valid desc, Check valid Crisis Rating, Check valid active date
&FN`TERRITORIES_CC.CHECK %wt=[setq(RET,1)][setq(T,[u(%vt/FN`VALID_TERRITORY,%0)])][if(not(%q<T>),[setq(RET,0)][pemit(%#,+territories: No such territory by that name.)])][if(not(%1),[setq(RET,0)][pemit(%#,+territories: Please set a name for the crisis.)])][if(not(%2),[setq(RET,0)][pemit(%#,+territories: Please set a description for the crisis.)])][if(not(date_valid(%4)),[setq(RET,0)][pemit(%#,+territories: Enter a valid active date.)])][if(not([u(%vt/FN`TERRITORIES.CRISIS.NAME_TO_TYPE,%5)]),[setq(RET,0)][pemit(%#,+territories: Crisis type must be one of SIMPLE\, CONTESTED\, RESISTED\, EXTENDED or THRESHOLD.)])]%q<RET>

&FN`TERRITORIES_CU.CHECK %wt=[setq(RET,1)][setq(T,[u(%vt/FN`TERRITORIES.VALID_CRISIS,%0)])][if(not(%q<T>),[setq(RET,0)][pemit(%#,+territories: No such crisis.)])][if(not(%1),[setq(RET,0)][pemit(%#,+territories: Please set a name for the crisis.)])][if(not(%2),[setq(RET,0)][pemit(%#,+territories: Please set a description for the crisis.)])][if(not([u(%vt/FN`TERRITORIES.CRISIS.NAME_TO_TYPE,%4)]),[setq(RET,0)][pemit(%#,+territories: Crisis type must be one of SIMPLE\, CONTESTED\, RESISTED\, EXTENDED or THRESHOLD.)])]%q<RET>

&FN`TERRITORIES_CS.CHECK %wt=[setq(RET,1)][setq(T,[u(%vt/FN`TERRITORIES.VALID_CRISIS,%0)])][if(not(%q<T>),[setq(RET,0)][pemit(%#,+territories: No such crisis.)])][SETQ(PC, [secure( [pmatch(%1)] )] )][if(not(%q<PC>),[setq(RET,0)][pemit(%2,+territories: No such player by that name.)])]%q<RET>

&FN`TERRITORIES_CC.RUN %wt=[sql([u(%vt/FN`TERRITORIES.INSERT_CRISIS_SQL,%0,%1,%2,%3,%4,[IF(%5,[u(%vt/FN`TERRITORIES.CRISIS.NAME_TO_TYPE,%5)],0)])])][pemit(%#,Crisis created.)]
&FN`TERRITORIES_CU.RUN %wt=[sql( [u(%vt/FN`TERRITORIES.UPDATE_CRISIS_SQL,%0,%1,%2,%3,[IF(%4,[u(%vt/FN`TERRITORIES.CRISIS.NAME_TO_TYPE,%4)],0)])])][pemit(%#,Crisis updated.)]

&FN`GENERATE_CRISIS %wt=[setq(0,[u(%wt/LIST`TERRITORIES_1_[rand(5)])])][setq(1,[u(%wt/LIST`TERRITORIES_2_[rand(2)])])][setq(2,[u(%wt/LIST`TERRITORIES_3_[rand(2)])])][extract(%q0,[add([rand([words(%q0,|)])],1)],1,|)]\,[space(1)][extract(%q1,[add([rand([words(%q1,|)])],1)],1,|)]\,[space(1)][extract(%q2,[add([rand([words(%q2,|)])],1)],1,|)]

&FN`TERRITORIES_CG %wt=[mapsql(FN`TERRITORIES_CG_ROW,[u(%vt/FN`TERRITORIES.SELECT_NEW_CRISES_SQL)])]%r[map(FN`TERRITORIES_CG.ADD_CRISES,%q<NEW_CRISES>,|)]
&FN`TERRITORIES_CG_ROW %wt=[setq(NUM_ROLLS,[sub(5,%3)])][setq(NUM_CRISES,[u(%ws/FN`ROLL_SILENT,10,%Q<NUM_ROLLS>)])][align(31 20,%1,%Q<NUM_CRISES> new crises)][setq(NEW_CRISES,[strcat(%q<NEW_CRISES>,%2 %Q<NUM_CRISES>|)])]%r

&FN`TERRITORIES_CG.ADD_CRISES %wt=[setq(TERRITORY_DBREF,[extract(%0,1)])][setq(TERRITORY_NUM_CRISES,[extract(%0,2)])][if([gt(%q<TERRITORY_NUM_CRISES>,0)],[setq(NEW_CRISES_TERR,repeat(%q<TERRITORY_DBREF>[space(1)],%q<TERRITORY_NUM_CRISES>))][iter(%Q<NEW_CRISES_TERR>,[u(%vt/FN`TERRITORIES.ADD_CRISIS,%q<TERRITORY_DBREF>)])])]

&TRIG`TERRITORIES.TODAY_TBD_CRISES %wt=[setq(NEW_CRISES,[sql([u(%vt/FN`TERRITORIES.SELECT_TBD_CRISES_SQL)],`,|)])][cemit(debug,Daily Crises Job creation triggered - [words(%q<NEW_CRISES>,`)] crises to process.,noisy)];@dol/inplace/delimit ` %q<NEW_CRISES>={[setq(1,[extract(%i0,1,1,|)])][setq(2,[extract(%i0,2,1,|)])][setq(3,[extract(%i0,3,1,|)])][setq(4,[extract(%i0,4,1,|)])]@trigger %vj/TRIG_CREATE=*#1,[u(%vj/FN_FIND-BUCKET,TERRA)],3,New Crisis for %q2,A new crisis for %q2 needs to be written up - the description is %q3\, and it will be active on %q4!%r%rPlease use +territories/cu %q1\=Name|Crisis Description|Rating|Type to fill out the details, where Type is one of SIMPLE\, CONTESTED\, RESISTED\, EXTENDED or THRESHOLD!}

&TRIG`TERRITORIES.MONTHLY_CRISES_GEN %wt=[cemit(debug,Monthly Crises generation triggered.,noisy)][u(%wt/FN`TERRITORIES_CG)]

&FN`TERRITORIES.SELECT_ACTIONS %wt=[pemit(%#,[align(6 15 20 5 20,ID,Type,By,Succs,Date)]%r)][mapsql(FN`TERRITORIES.SELECT_ACTIONS_ROW,[u(%vt/[if(%1,FN`TERRITORIES.SELECT_ACTIONS_LAST_WEEK_SQL,FN`TERRITORIES.SELECT_ACTIONS_WEEK_SQL)],"%0")])]
&FN`TERRITORIES.SELECT_ACTIONS_ROW %wt=[pemit(%#,[align(6 15 20 5 20,%1,[u(%vt/FN`TERRITORIES.ACTION.TYPE_TO_NAME,%2)],[name(#%3)],%4,%5)])]

&FN`TERRITORIES.SELECT_CRISES %wt=[pemit(%#,[header(Crises for [ucstr(%0)])]%r[align(-6 10 62,ID,Date Active On,Name)]%r)][mapsql(FN`TERRITORIES.SELECT_CRISES_ROW,[u(%vt/FN`TERRITORIES.SELECT_CRISES_SQL,"%0")])]
&FN`TERRITORIES.SELECT_CRISES_ROW %wt=[pemit(%#,[align(>6 10 62,[IF(%6,[ansi(hr,*)])]%1,[left(%4,10)],%2 \(%5\, [u(%vt/FN`TERRITORIES.CRISIS.TYPE_TO_NAME,%7)]\))]%r%3%r)]

&FN`TERRITORIES.SELECT_TBD_CRISES %wt=[pemit(%#,[header(Pending Crises)]%r)][pemit(%#,[align(-6 18 40 10,ID,Territory,Description,Date Active on)]%r)][mapsql(FN`TERRITORIES.SELECT_TBD_CRISES_ROW,[u(%vt/FN`TERRITORIES.SELECT_TBD_CRISES_ALL_SQL)])]
&FN`TERRITORIES.SELECT_TBD_CRISES_ROW %wt=[pemit(%#,[align(>6 18 40 10,[IF(%5,[ansi(hr,*)])]%1,%2,%3,[left(%4,10)])])]

&FN`TERRITORIES.SET_KINGPIN %wt=[SETQ(KP,[trim(num(%1),#)])][sql([u(%vt/FN`TERRITORIES.SET_KINGPIN_SQL,%0,[IF(gt(%Q<KP>,0),%Q<KP>,NULL)])])]
&FN`TERRITORIES.SET_STAT %wt=[sql([u(%vt/FN`TERRITORIES.SET_STAT_SQL,%0,%1,%2)])]

&WT %wt=#2279
&VT %wt=#1246
&WS %wt=#1770
&WH %wt=#2062
&WD %wt=#2930
&VJ %wt=#18