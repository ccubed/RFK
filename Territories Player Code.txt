@create Territories Player Code
@link Territories Player Code = #2
@set Territories Player Code = !NO_COMMAND
@power Territories Player Code=SQL_OK

@@ Data
&DATA`SECSINDAY %we=86400

&CMD`TERR_PATROL %we=$+terr/patrol *:[if([u(FN`TERR_PATROL.CHECK,%#,%0)],[u(%vt/FN`TERRITORIES.ACTION.PATROL_RUN,%#,%q<T>,%0)])]

&FN`TERR_PATROL.CHECK %we=[setq(RET,1)][setq(T,[u(%vt/FN`VALID_TERRITORY,%1)])][if(not(%q<T>),[setq(RET,0)][pemit(%0,+terr: No such territory by that name.)])]%q<RET>

@@ Automated Commands

@@ Basic Investigation - 3 steps: INVESTIGATE_BASIC, PATROL_CHECK, MAIL_REPORT (Triggers to escape invocation limits)
&CMD`TERR_INVESTIGATE_BASIC %we=$+territories/investigate *=*/*:@assert [u(%vt/FN`VALID_TERRITORY,%0)]=@nspemit/silent %#=+territories: No such territory by that name;@assert [gt(get_skill(%#,%1),0)]=@nspemit/silent %#=+territories: You aren't trained in that skill or it's not a real one.;@assert [match(Politics Streetwise Empathy Investigation,%1)]=@nspemit/silent %#=+territories: You can only investigate with Streetwise, Politics, Empathy or Investigation;@assert [lte(%2,[add([get_attrib(%#,Wits)],[get_skill(%#,%1)])])]=@nspemit/silent %#=+territories: You can only roll a number of times equal to your attribute+skill.;@assert [setq(WK_REM,[u(#2930/FN`WEEKLY_DOWNTIME_REMAINING,%#)])][setq(DA_REM,[u(#2930/FN`DAILY_DOWNTIME_REMAINING,%#)])][and(gte(sub(%q<WK_REM>,add(1,mul([if(get_merit(%#,Good Time Management),1,2)],%2))),0),gte(sub(%q<DA_REM>,add(1,mul([if(get_merit(%#,Good Time Management),1,2)],%2))), 0))]=@nspemit/silent %#=+territories: You don't have enough downtime or it exceeds your daily limit. .;[setq(0,[add([get_attrib(%#,Wits)],[get_skill(%#,%1)],[get_discipline(%#,Auspex)],[sql(SELECT information FROM territory WHERE name='[sqlescape(%0)]')],[ulocal(FN`CHECK_INVESTIGATION_DISCIPLINES,%#)])])][u(#3376/F.ROLL,%q0,[switch(get_merit(%#,Trained Observer),1,9,3,8,10)],0,0,0,%2,0)];[u(#2930/FN`INSERT,%#,[add(1,mul([if(get_merit(%#,Good Time Management),1,2)],%2))],Investigate %0)][u(%vt/FN`TERRITORIES.ADD_ACTION,[sql(SELECT dbref FROM territory WHERE name='[sqlescape(%0)]')],[u(%vt/FN`TERRITORIES.ACTION.NAME_TO_TYPE,Investigate)],[trim(%#,#)],%qS,[sql_date_time()])];@trig #3745/TRIG`PATROL_CHECK=%#,%0,%qS

&TRIG`PATROL_CHECK %we=[setq(actions,[sql(u(%vt/FN`TERRITORIES.SELECT_ACTIONS_WEEK_SQL,'%1',1),|,~)])][setq(temp,0)][iter(%q<actions>,[if(eq(extract(%i0,2,1,~),1),setq(temp,[extract(%i0,4,1,~)]|[extract(%i0,3,1,~)]))],|)][setq(patrol,0)][if(and(%q<temp>,not(strmatch(%0,#[after(%q<temp>,|)]))),[setq(patrol,[ulocal(FN`DECIDE_PATROL,%0,%q<temp>,%1)])])];@switch %q<patrol>=0,{@trig #3745/TRIG`TERRITORIES.MAIL_REPORT=%0,%1,%2},{@trig #18/TRIG_CREATE=#3745,[setr(bdb,[u(#3287/F`GRAB_BUCKET,[trim([u(#3287/F`FIND_AREA,%1)])])])],2,Investigation Intercepted,[name(%0)] was intercepted while investigating %1 by [name([setr(ron,#[sql(select player_dbref from retainers where name='[name(%q<patrol>)]')])])] who is currently patrolling this territory. This gives [name(%q<patrol>)] the chance to intercept them on screen or allow them to continue with their investigation. Please discuss amongst yourselves what you wish to do.%R%R[name(%0)] rolled %2 Successes on their roll.,%0 %q<ron>,[xget(%q<bdb>,staffer)]}

&TRIG`TERRITORIES.MAIL_REPORT %we=[setq(c,[sql(u(%vt/FN`TERRITORIES.SELECT_PLAYER_INVESTIGATION_CRISES,%1),|,~)])][setq(d,[if(strmatch(after(%0,#),[extract([extract(%qC,1,1,|)],12,1,~)]),2,3)])][setq(msg,Territory Report for %1%R%R)];@switch [setq(p,%2)][gt(div(%2,%qD),0)][gt(sub(%2,1),0)]=1*,{[u(FN`FORMAT_CRISIS,%qC,%qP,%qD)][setq(p,[sub(%qP,mul([if(words(%qC,|),[if(lte([div(%qP,%qD)],words(%qC,|)),[div(%qP,%qD)],words(%qC,|))],0)],%qD))])][if(gte(%qP,1),[u(FN`FEEDING_DATA,%qP,%1)])];@mail %0=Investigation of %1 by [name(%0)]/%RSuccesses: %2%R%R%q<msg>},01,{[u(FN`FEEDING_DATA,%qP,%1)];@mail %0=Investigation of %1 by [name(%0)]/%RSuccesses:%2%R%R%q<msg>},{@mail %0=Investigation of %1 by [name(%0]/%q<msg>Successes: %2%R%RUnfortunately you didn't roll enough successes to gather any information.}

@@ Functions For Formatting
&FN`FORMAT_CRISIS %we=[setq(msg,%q<msg>Crises%R)][if(words(%qC,|),[iter(lnum(1,[if(lte([div(%1,%2)],words(%qC,|)),[div(%1,%2)],[words(%qC,|)])]),[setq(i,extract(%0,%i0,1,|))][setq(msg,%q<msg>[extract(%qI,3,1,~)]%b\([extract(%qI,5,1,~)]\, [u(%vt/FN`TERRITORIES.CRISIS.TYPE_TO_NAME,[extract(%qI,6,1,~)])]\)%R[extract(%qI,4,1,~)]%R%R)])])]

&FN`FEEDING_DATA %we=[setq(msg,%q<msg>%R%RFeeding Data%R)][setq(c,[sql(u(%vt/FN`TERRITORIES.SELECT_FEEDING_DATA_PLAYER_INVESTIGATION,%1,%0),|,~)])][if(words(%qC),[iter(%qC, [setq(msg,%q<msg>[name(#[extract(%i0,1,1,~)])]%R)],|)],[setq(msg,%q<msg>%RNo recent feedings.)])]


@@ Functions for Determining Dice Pools
&FN`CHECK_INVESTIGATION_DISCIPLINES %we=[floor(fdiv([max(get_discipline(%0,Animalism),get_discipline(%0,Dominate),get_discipline(%0,Majesty),get_discipline(%0,Protean))],2))]

&FN`DECIDE_PATROL %we=[switch(hasattr(#[after(%1,|)],owner),1,[setq(o,xget(#[after(%1,|)],owner))],[setq(o,#[after(%1,|)])])][setq(0,add(get_attrib(%0,Wits),max(get_skill(%0,Streetwise),get_skill(%0,Stealth)),get_discipline(%0,Obfuscate),[sql(SELECT access FROM territory WHERE name='[sqlescape(%2)]')]))][u(#3376/F.ROLL,%q0,10,0,0,0,0,0)][if(lt(%qS,before(%1,|)),%qo,0)]

@@ OSS Player Action Commands Automation
@@ Start - Form Begins (#3987 = OSS Automation String/Message Database)
&CMD`OSS_PLAYER_AUTOMATION_TYPE %we=$+oss/type *:@assert [match(Coverup Patrol,%0)]={@pemit %#=[ansi(hw,OSS:)]%bThe system doesn't currently support that type of automated OSS request. Please use +request.};@set %#=FORM:;@set %#/FORM=wizard;@set %#=FORM`TYPE:%0;@trig #3745/TRIG`PREP_%0=%#

&CMD`OSS_PLAYER_AUTOMATION_OSS %we=$+oss:@pemit %#=[u(#3987/ST_OSS)]
&CMD`OSS_PLAYER_AUTOMATION_OSS_TOPIC %we=$+oss *:@assert [hasattrval(#3987,ST_%0)]={@pemit %#=[ansi(hw,OSS:)]%bCouldn't find a topic named %0.};@pemit %#=[u(#3987/ST_%0)]

&CMD`OSS_PLAYER_AUTOMATION_SKILL %we=$+oss/skill *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@switch [xget(%#,FORM`TYPE)]=*coverup,{@switch [match(Subterfuge Larceny Streetwise,%0)]=0,[pemit(%#,[ansi(hw,OSS:)]%bCoverups can only use Subterfuge\, Larceny or Streetwise.)],[attrib_set(%#/FORM`SKILL,%0)][attrib_set(%#/FORM`SITES,0)][attrib_set(%#/FORM`SITE)][pemit(%#,[ansi(hw,OSS:)]%bSkill set to %0. Sites cleared. Use [ansi(hw,+form)] to review the form.)]}

&CMD`OSS_PLAYER_AUTOMATION_SPEC %we=$+oss/spec:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@switch [xget(%#,FORM`SPEC)]=0,[attrib_set(%#/FORM`SPEC,1)],1,[attrib_set(%#,FORM`SPEC,2)],2,[attrib_set(%#/FORM`SPEC,0)],[attrib_set(%#/FORM`SPEC,1)];@pemit %#=[ansi(hw,OSS:)]%bSpecialty use toggled.

&CMD`OSS_PLAYER_AUTOMATION_RP %we=$+oss/rp *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@assert [cand(isint(%0),lte(%0,5))]={@pemit %#=[ansi(hw,OSS:)]%bEither you didn't give a number or you attempted to get more than the max bonus of +5 for RP.};@set %#=FORM`RP:%0;@pemit %#=[ansi(hw,OSS:)]%bYou have elected to receive a bonus of %0 and spend [switch(%0,1,1,2,4,3,10,4,20,5,40)]RP per roll.

&CMD`OSS_PLAYER_AUTOMATION_PT %we=$+oss/pt *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@assert [cand(isint(%0),lte(%0,2))]={@pemit %#=[ansi(hw,OSS:)]%bEither you didn't give a number or didn't use 1 or 2.};@set %#=FORM`PT:[add(xget(%#,FORM`PT),[switch(%0,1,2,2,3)])];@pemit %#=[ansi(hw,OSS:)]%b[switch(%0,1,Toggled 9-again for PT,2,Toggled 9-again and Rote for PT)]

&CMD`OSS_PLAYER_AUTOMATION_RETAINERS %we=$+oss/retainer:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@assert [setr(A,[ulocal(%vt/FN`RETAINERS.SQL_CHECK_FREE_ACTIONS,trim(%#,#))])]={@pemit %#=[ansi(hw,OSS:)]%bIt doesn't look like any of your retainers have defensive actions left this week.};[setq(B,[xget(%#,FORM`TYPE)])][iter(lattr(%#/FORM**),[attrib_set(%#/%i0)])][attrib_set(%#/FORM)];@set %#=FORM`TYPE:RET_%qB;@set %#=FORM`RETAINERS:%qA;@switch %qB=Patrol,{@set %#=FORM`SKILL:Streetwise;},Coverup,{@set %#=FORM`SKILL:Undetermined;};@pemit %#=[u(#3987/FORMS_[xget(%#,FORM`TYPE)])]

@@ Retainer Select. OSS.CHECK.RETAINER sets data
&CMD`OSS_PLAYER_AUTOMATION_SELRET %we=$+oss/selret *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@assert [strmatch(xget(%#,FORM`TYPE),RET*)]={@pemit %#=[ansi(hw,OSS:)]%bYou only use this command for retainer forms.};@assert [match(xget(%#,FORM`RETAINERS),%0)]={@pemit %#=[ansi(hw,OSS)]:%bThat isn't a valid retainer. Please see +oss};[setq(A,sql(SELECT * FROM retainers WHERE id=[sqlescape(%0)],|,~))][u(FN`OSS.CHECK.RETAINER.[xget(%#,FORM`TYPE)],%qA,%#)];@set %#=FORM`RETAINER:%0[chr(58)][sql(select name from retainers where id=[sqlescape(%0)])];@fo %#=+form

&CMD`OSS_PLAYER_AUTOMATION_CLEAR %we=$+oss/clear:[iter(lattr(%#/FORM**),[attrib_set(%#/%i0)])][attrib_set(%#/FORM)];@pemit %#=[ansi(hw,OSS:)]%bCurrent form cleared.

&CMD`OSS_PLAYER_AUTOMATION_RETAINER_TW %we=$+oss/retainertw:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};[setq(A,[sql(SELECT id\,name\,rank FROM retainers WHERE player_dbref=[trim(%#,#)],|,~)])];@pemit %#=[u(#3987/ST_RET_TW,%qA)]

&CMD`OSS_PLAYER_AUTOMATION_RETAINER_TW_SELECT %we=$+oss/retainertw *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@assert [strmatch(trim(%#,#),sql(select player_dbref from retainers where id=[sqlescape(%0)]))]={@pemit %#=[ansi(hw,OSS:)]%bThat's not your retainer.};[setq(A,sql(select rank from retainers where id=[sqlescape(%0)]))];@switch [u(FN`CHECK_TAG_SKILL,%0,%#)]=1,{@set %#=FORM`RETTW:[mul(%qA,2)][chr(58)][ceil(fdiv(%qA,2))]},{@set %#=FORM`RETTW:%qA[chr(58)][ceil(fdiv(%qA,2))]};[attrib_set(%#/FORM`RETAINERS)];@fo %#=+oss

&CMD`OSS_PLAYER_AUTOMATION_SUBMIT %we=$+oss/submit:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@break [cand([not(strmatch(xget(%#,FORM`TYPE),RET*))],[strmatch(xget(%#,FORM`SKILL),Undetermined)])]={@pemit %#=[ansi(hw,OSS:)]%bYou at least need to pick a skill to use.};@break [cand(strmatch(xget(%#,FORM`TYPE),*Patrol),not(hasattrval(%#,FORM`TERRITORY)))]={@pemit %#=[ansi(hw,OSS:)]%bYou haven't selected a territory but you're putting in a patrol action.};@trig #3745/TRIG`PROC_[xget(%#,FORM`TYPE)]=%#

&CMD`OSS_PLAYER_AUTOMATION_SITES %we=$+oss/sites:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@break [strmatch(xget(%#,FORM`SKILL),undetermined)]={@pemit %#=[ansi(hw,OSS:)]%bBefore using a site, please select a skill.};@pemit %#=[u(#3987/ST_SITES_HELP)]

&CMD`OSS_PLAYER_AUTOMATION_SUBTYPE %we=$+oss/subtype *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@switch 1=[strmatch(xget(%#,FORM`TYPE),*COVERUP)],[if([match(movement feeding,%0)],[attrib_set(%#/FORM`SUBTYPE,%0)][pemit(%#,[ansi(hw,OSS:)]%bSubtype set to %0)],[pemit(%#,[ansi(hw,OSS:)]%bSubtype for coverup can only be movement or feeding.)])]

&CMD`OSS_PLAYER_AUTOMATION_SELTER %we=$+oss/ter *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@assert [u(%vt/FN`VALID_TERRITORY,%0)]={@pemit %#=[ansi(hw,OSS:)]%b%0 is not a valid territory.};@set %#=FORM`TERRITORY:%0;@pemit %#=[ansi(hw,OSS:)]%bTerritory set to %0.

&CMD`OSS_PLAYER_AUTOMATION_FORM %we=$+form:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@pemit %#=[u(#3987/FORMS_[xget(%#,FORM`TYPE)])]

@@ %qA - site name, %qB - full site listing from DB, %qC - Index of skill in returned query concat, %qD - modifier, %qE - Access
&CMD`OSS_PLAYER_AUTOMATION_SITES_INCLUDE %we=$+oss/sites *:@assert [hasattrval(%#,FORM`TYPE)]={@fo %#=+oss};@break [hasattrval(%#,FORM`SITE)]={@pemit %#=[ansi(hw,OSS:)]%bYou've already selected a site.};@assert [setr(A,[sql(u(%vt/FN`SITES.GET_SITE_NAME_BY_ID,%0))])]={@pemit %#=[ansi(hw,OSS:)]%bDouble check the ID. That didn't return a result.};[setq(B,[sql(u(%vt/FN`SITES.GET_SITE_NAME_MODIFIERS_NAME,%qA),|,~)])];@assert [cor([setr(c,[match(extract(%qB,3,1,~),xget(%#,FORM`SKILL),:)])],strmatch(xget(%#,FORM`TYPE),RET*))]={@pemit %#=[ansi(hw,OSS:)]%bThat site doesn't have a skill that's applicable to the action you're taking.};[setq(D,extract(extract(%qB,4,1,~),[if(t(%qC),%qC,1)],1,:))];@assert [lte(add(default(%#/FORM`SITES,0),%qD),5)]={@pemit %#=[ansi(hw,OSS:)]%bMax Bonus is +5.};[setq(e,[u(FN`CAN_ACCESS,%#,%qB)])];@set %#=FORM`SITES:[add(default(%#/FORM`SITES,0),[if(%qE,%qD,bound(div(%qD,2),1,5))])];@set %#=FORM`SITE:[setunion(xget(%#,FORM`SITE),extract(%qB,2,1,~))];@pemit %#=[ansi(hw,OSS:)]%bUsing %qA for a +[if(%qE,%qD,bound(div(%qD,2),1,5))] bonus to [xget(%#,FORM`SKILL)].[if(not(%qE),%bYou didn't have access to the site so the bonus was halved.)]

&CMD`OSS_PLAYER_AUTOMATION_SITES_SEARCH_INFLUENCE %we=$+sites/influence *:@assert [match(Bureaucracy|Church|Education|Finance|Health|High Society|Industry|Legal|Media|Occult|Police|Politics|Street|Transportation|Underworld,%0,|)]={@pemit %#=[ansi(hw,OSS:)]%bThat's not a valid influence category. See +help influence.};@pemit %#=[u(#3987/ST_SITES_LISTING,[sql(u(%vt/FN`SITES.GET_SITE_NAME_MODIFIERS_INFL,%0),|,~)],[hasattrval(%#,FORM`TYPE)])]

&CMD`OSS_PLAYER_AUTOMATION_SITES_SEARCH_TERRITORY %we=$+sites/territory *:@assert [setr(A,[u(%vt/FN`VALID_TERRITORY,%0)])]={@pemit %#=[ansi(hw,OSS:)]%b%0 is not a valid territory name.};@pemit %#=[u(#3987/ST_SITES_LISTING,[sql(u(%vt/FN`SITES.GET_SITE_NAME_MODIFIERS_TERRITORY,%qA),|,~)],[hasattrval(%#,FORM`TYPE)])]

&CMD`OSS_PLAYER_SITE_TO_JOB %we=$+sites/job *=*:@break [u(#18/FN_GUEST,%#)]={@pemit %#=This command is not available to guests.};@assert [isdbref(setr(0,u(#18/FN_FIND-JOB,%1)))]={@pemit %#=Can't find that job.};@assert [match(xget(%q0,opened_by),%#)]={@pemit %#=That's not your job!};@assert [setr(A,[sql(u(%vt/FN`SITES.GET_SITE_NAME_BY_ID,%0))])]={@pemit %#=[ansi(hw,OSS:)]%bDouble check the ID. That didn't return a result.};[setq(B,[sql(u(%vt/FN`SITES.GET_SITE_NAME_MODIFIERS_NAME,%qA),|,~)])][setq(e,[u(FN`CAN_ACCESS,%#,%qB)])];@trigger #18/TRIG_ADD=%q0,[name(%#)] has used a site to help perform the relevant task.%R%RSite: [extract(%qB,1,1,~)]%RSkills: [extract(%qB,3,1,~)]%RModifiers: [extract(%qB,4,1,~)]%ROpen Site: [if(extract(%qB,5,1,~),Yes,No)]%RPlayer Has Access: [if(%qE,Yes,No)],%#,ADD;@trigger #18/TRIG_BROADCAST=%q0,%#,ADD;@pemit %#=You add a site to [name(%q0)];[u(%vt/FN`TERRITORIES.ADD_ACTION,sql(SELECT territory_dbref FROM territory_site WHERE id=%0),5,trim(%#,#),0,[timefmt($Y-$m-$d $H:$M:$S,[setr(x,[secs()])])])];@trig #3745/TRIG`NOTIFY_SITE_USAGE=%#,%qX,[sql(select territory_dbref from territory_site where id=%0)],[extract(%qB,1,1,~)],[extract(%qB,5,1,~)]

@@ Passed - %0 Player DBREF, %1 - Time it happened, %2 - Territory dbref of the site, %3 - Name of the Site, %4 - open or closed (1 for open, 0 for closed)
&TRIG`NOTIFY_SITE_USAGE

@@ Preperation Triggers
@@ Coverup - %qA (retainers), %qB (highest of subterfuge, streetwise, larceny or undetermined), %qC (Dominate), %qD (Nightmare), %qE (Obfuscate), %qF (Alt ID), %qG (Small Framed), %qH (PT)
&TRIG`PREP_COVERUP %we=[setq(A,[sql(u(%vt/FN`RETAINERS.SQL_PLAYER_GET_RETAINERS,trim(%0,#)))])][setq(B,[switch([gt(get_skill(%0,subterfuge),get_skill(%0,streetwise))][gt(get_skill(%0,streetwise),get_skill(%0,Larceny))][gt(get_skill(%0,Subterfuge),get_skill(%0,Larceny))],11*,Subterfuge,101,Subterfuge,100,Larceny,01*,Streetwise,001,Subterfuge,Undetermined)])][setq(C,get_discipline(%0,Dominate))][setq(D,get_discipline(%0,Nightmare))][setq(E,get_discipline(%0,Obfuscate))][setq(F,get_merit(%0,Alternate Identity))][setq(G,[if(get_merit(%0,Small-Framed),2,0)])][setq(H,[switch(get_merit(%0,Professional Training),5,2,>=2,1,0)])];@set %0=FORM`RETAINERS:[if(%qA,1,0)];@set %0=FORM`SKILL:%qB;@set %0=FORM`DISC.DOM:%qC;@set %0=FORM`DISC.NIGHT:%qD;@set %0=FORM`DISC.OBF:%qE;@set %0=FORM`ALTID:%qF;@set %0=FORM`SMALLF:%qG;@set %0=FORM`PT:%qH;@fo %0=+form

&TRIG`PREP_PATROL %we=[setq(A,[sql(u(%vt/FN`RETAINERS.SQL_PLAYER_GET_RETAINERS,trim(%0,#)))])][setq(B,Streetwise)][setq(C,[switch(get_merit(%0,Professional Training),5,2,>=2,1,0)])];@set %0=FORM`RETAINERS:[if(%qA,1,0)];@set %0=FORM`SKILL:%qB;@set %0=FORM`PT:%qC;@fo %0=+form

@@ Processing Triggers
&TRIG`PROC_COVERUP %we=@assert [hasattrval(%0,FORM`SUBTYPE)]={@pemit %0=[ansi(hw,OSS:)]%bCoverups require a subtype. Please see +oss.};[setq(A,[add([switch(xget(%0,FORM`SKILL),Subterfuge,[add(get_attrib(%0,Manipulation),get_discipline(%0,Majesty))],Streetwise,[add(get_attrib(%0,wits),div(get_discipline(%0,Protean),2),div(get_discipline(%0,majesty),2))],[add(get_attrib(%0,Wits),div(get_discipline(%0,Protean),2))])],get_skill(%0,xget(%0,FORM`SKILL)),xget(%0,FORM`ALTID),xget(%0,FORM`DISC.DOM),xget(%0,FORM`DISC.NIGHT),xget(%0,FORM`DISC.OBF),xget(%0,FORM`SMALLF),default(%0/FORM`RP,0),default(%0/FORM`SITES,0),default(%0/FORM`SPEC,0))])][u(#3376/F.ROLL,%qA,if(gte(xget(%0,FORM`PT),3),9,10),if(eq(xget(%0,FORM`PT),5),1,0),0,0,0,0)];@pemit %0=[header(Coverup [xget(%0,FORM`SUBTYPE)])]%RYou have rolled %qA dice for %qS successes on your coverup. This will apply as a threshold to anyone attempting to investigate your [xget(%0,FORM`SUBTYPE)] until [timefmt($m-$d-$Y,[setr(ends,[add([setr(now,secs())],mul(xget(%we,DATA`SECSINDAY),7))])])].;@trig #3745/TRIG`PROC_CLEANUP_[xget(%0,FORM`TYPE)]=%0,%q<now>,%q<ends>,%qS

&TRIG`PROC_RET_COVERUP %we=@assert [hasattrval(%0,FORM`SUBTYPE)]={@pemit %0=[ansi(hw,OSS:)]%bCoverups require a subtype. Please see +oss.};[setq(A,add(if(after(xget(%0,FORM`RET.SKILL),:),mul(before(xget(%0,FORM`RET.SKILL),:),2),before(xget(%0,FORM`RET.SKILL),:)),xget(%0,FORM`DISC.DOM),xget(%0,FORM`DISC.NIGHT),xget(%0,FORM`DISC.OBF),default(%0/FORM`RP,0),default(%0/FORM`SITES,0)))][u(#3376/F.ROLL,%qA,10,0,0,0,0,0)];@pemit %0=[header(Coverup [xget(%0,FORM`SUBTYPE)])]%RYour retainer rolled %qA dice for %qS successes on its coverup. This will apply as a threshold to anyone attempting to investigate your [xget(%0,FORM`SUBTYPE)] until [timefmt($m-$d-$Y,[setr(ends,[add([setr(now,secs())],mul(xget(%we,DATA`SECSINDAY),7))])])].;@trig #3745/TRIG`PROC_CLEANUP_[xget(%0,FORM`TYPE)]=%0,%q<now>,%q<ends>,%qS

&TRIG`PROC_PATROL %we=[setq(A,[add(get_attrib(%0,Wits),get_skill(%0,Streetwise),[mul(-1,sql(select awareness from territory where name='[xget(%0,FORM`TERRITORY)]'))],default(%0/FORM`RP,0),default(%0/FORM`SITES,0),default(%0/FORM`SPEC,0))])][u(#3376/F.ROLL,%qA,if(gte(xget(%0,FORM`PT),3),9,10),if(eq(xget(%0,FORM`PT),5),1,0),0,0,0,0)];@pemit %0=[header(Patrol [xget(%0,FORM`TERRITORY)])]%RYou have rolled %qA Dice for %qS successes on your patrol. You will be informed of crises as they pop and your successes will count as a challenge to being informed automatically of anyone operating in the territory. This will last until [timefmt($m-$d-$Y,[setr(ends,[add([setr(now,secs())],mul(xget(%we,DATA`SECSINDAY),7))])])].;@trig #3745/TRIG`PROC_CLEANUP_[xget(%0,FORM`TYPE)]=%0,%q<now>,%q<ends>,%qS

&TRIG`PROC_RET_PATROL %we=[setq(A,add(if(after(xget(%0,FORM`RET.SKILL),:),mul(before(xget(%0,FORM`RET.SKILL),:),2),before(xget(%0,FORM`RET.SKILL),:)),default(%0/FORM`RP,0),default(%0/FORM`SITES,0)))][u(#3376/F.ROLL,%qA,10,0,0,0,0,0)];@pemit %0=[header(Patrol [xget(%0,FORM`TERRITORY)])]%RYour retainer rolled %qA dice for %qS successes on its patrol. You will be informed of crises as they pop and the successes will count as a challenge to being informed automatically of anyone operating in the territory. This will last until [timefmt($m-$d-$Y,[setr(ends,[add([setr(now,secs())],mul(xget(%we,DATA`SECSINDAY),7))])])].;@trig #3745/TRIG`PROC_CLEANUP_[xget(%0,FORM`TYPE)]=%0,%q<now>,%q<ends>,%qS

@@ Clean Up Triggers - Always passed DBREF, time now, time ends, sux
@@ Charge DT (Retainer TW, Base Hours), Site Usage Entries (Pools and Territories), Insert Action in Player/Retainer Action tables, Charge RP, Charge WP if Rote
&TRIG`PROC_CLEANUP_COVERUP %we=[sql(u(%vt/FN`PLAYER.ACTIONS.INSERT_ROW,[switch(xget(%0,FORM`SUBTYPE),feeding,1,4)],Cover up [xget(%0,FORM`SUBTYPE)],[timefmt($Y-$m-$d $H:$M:$S,%1)],[timefmt($Y-$m-$d $H:$M:$S,%2)],trim(%0,#),trim(%0,#),%3))][setq(dt,add(7,if(hasattrval(%0,FORM`RETTW),after(xget(%0,FORM`RETTW),:),0)))][setq(rp,switch(default(%0/FORM`RP,0),1,1,2,4,3,10,4,20,5,40,0))][u(#2930/FN`INSERT,%0,%q<dt>,Cover up [xget(%0,FORM`SUBTYPE)])][if(%q<rp>,attrib_set(%0/money,[extract(xget(%0,money),1,1,|)]|[sub(extract(xget(%0,money),2,1,|),%q<rp>)]))][if(hasattrval(%0,FORM`SITE),[u(%vt/FN`TERRITORIES.ADD_ACTION,setr(tdb,sql(SELECT territory_dbref FROM territory_site WHERE id=[xget(%0,FORM`SITE)])),5,trim(%0,#),0,[timefmt($Y-$m-$d $H:$M:$S,%1)])])][iter(lattr(%0/FORM**),attrib_set(%0/%i0))][attrib_set(%0/FORM)];@switch [hasattrval(%0,FORM`SITE)]=1,{@trig #3745/TRIG`NOTIFY_SITE_USAGE=%0,%1,%q<tdb>,[sql(select name from territory_site where id=[xget(%0,FORM`SITE)])],[sql(select open from territory_site where id=[xget(%0,FORM`SITE)])]};@pemit %0=[ansi(hw,OSS:)]%bYour cover up has been completed successfully. Thanks for using the automation tools.

&TRIG`PROC_CLEANUP_RET_COVERUP %we=[sql(u(%vt/FN`RETAINERS.SQL_INSERT_ACTION,[before(xget(%0,FORM`RETAINER),:)],[switch(xget(%0,FORM`SUBTYPE),movement,1,feeding,2)],trim(%0,#),Coverup [xget(%0,FORM`SUBTYPE)] for [name(%0)],[timefmt($Y-$m-$d $H:$M:$S,%1)],[timefmt($Y-$m-$d $H:$M:$S,%2)],%3))][setq(rp,switch(default(%0/FORM`RP,0),1,1,2,4,3,10,4,20,5,40,0))][if(%q<rp>,attrib_set(%0/money,[extract(xget(%0,money),1,1,|)]|[sub(extract(xget(%0,money),2,1,|),%q<rp>)]))][if(hasattrval(%0,FORM`SITE),[u(%vt/FN`TERRITORIES.ADD_ACTION,setr(tdb,sql(SELECT territory_dbref FROM territory_site WHERE id=[xget(%0,FORM`SITE)])),5,trim(locate(#2936,after(xget(%0,FORM`RETAINER),:),T),#),0,[timefmt($Y-$m-$d $H:$M:$S,%1)])])][iter(lattr(%0/FORM**),attrib_set(%0/%i0))][attrib_set(%0/FORM)];@switch [hasattrval(%0,FORM`SITE)]=1,{@trig #3745/TRIG`NOTIFY_SITE_USAGE=%0,%1,%q<tdb>,[sql(select name from territory_site where id=[xget(%0,FORM`SITE)])],[sql(select open from territory_site where id=[xget(%0,FORM`SITE)])]};@pemit %0=[ansi(hw,OSS:)]%bYour retainer's defensive coverup has been completed successfully. Thanks for using the automation tools!

&TRIG`PROC_CLEANUP_PATROL %we=[setq(dt,add(7,if(hasattrval(%0,FORM`RETTW),after(xget(%0,FORM`RETTW),:),0)))][setq(rp,switch(default(%0/FORM`RP,0),1,1,2,4,3,10,4,20,5,40,0))][u(#2930/FN`INSERT,%0,1,Patrol [xget(%0,FORM`TERRITORY)])][u(FN`INSERT_PATROL_DT_RECORD,%0,sub(%q<dt>,1),Patrol [xget(%0,FORM`TERRITORY)])][if(%q<rp>,attrib_set(%0/money,[extract(xget(%0,money),1,1,|)]|[sub(extract(xget(%0,money),2,1,|),%q<rp>)]))][u(%vt/FN`TERRITORIES.ADD_ACTION,sql(select dbref from territory where name='[xget(%0,FORM`TERRITORY)]'),1,trim(%0,#),%3,[timefmt($Y-$m-$d $H:$M:$S,%1)])][if(hasattrval(%0,FORM`SITE),[u(%vt/FN`TERRITORIES.ADD_ACTION,setr(tdb,sql(SELECT territory_dbref FROM territory_site WHERE id=[xget(%0,FORM`SITE)])),5,trim(%0,#),0,[timefmt($Y-$m-$d $H:$M:$S,%1)])])][iter(lattr(%0/FORM**),attrib_set(%0/%i0))][attrib_set(%0/FORM)];@switch [hasattrval(%0,FORM`SITE)]=1,{@trig #3745/TRIG`NOTIFY_SITE_USAGE=%0,%1,%q<tdb>,[sql(select name from territory_site where id=[xget(%0,FORM`SITE)])],[sql(select open from territory_site where id=[xget(%0,FORM`SITE)])]};@pemit %0=[ansi(hw,OSS:)]%bYour patrol has been recorded successfully. Thanks for using the automation tools.

&TRIG`PROC_CLEANUP_RET_PATROL %we=[setq(dbn,[lsearch(all,name,[after([xget(%0,FORM`RETAINER)],:)])])][setq(rp,switch(default(%0/FORM`RP,0),1,1,2,4,3,10,4,20,5,40,0))][if(%q<rp>,attrib_set(%0/money,[extract(xget(%0,money),1,1,|)]|[sub(extract(xget(%0,money),2,1,|),%q<rp>)]))][sql(u(%vt/FN`RETAINERS.SQL_INSERT_ACTION,[before(xget(%0,FORM`RETAINER),:)],3,sql(select dbref from territory where name='[xget(%0,FORM`TERRITORY)]'),Patrol [xget(%0,FORM`TERRITORY)],[timefmt($Y-$m-$d $H:$M:$S,%1)],[timefmt($Y-$m-$d $H:$M:$S,%2)],%3))][u(%vt/FN`TERRITORIES.ADD_ACTION,sql(select dbref from territory where name='[xget(%0,FORM`TERRITORY)]'),1,if(%q<dbn>,trim(%q<dbn>,#),trim(%0,#)),%3,[timefmt($Y-$m-$d $H:$M:$S,%1)])][if(hasattrval(%0,FORM`SITE),[u(%vt/FN`TERRITORIES.ADD_ACTION,setr(tdb,sql(SELECT territory_dbref FROM territory_site WHERE id=[xget(%0,FORM`SITE)])),5,trim(locate(#2936,after(xget(%0,FORM`RETAINER),:),T),#),0,[timefmt($Y-$m-$d $H:$M:$S,%1)])])][iter(lattr(%0/FORM**),attrib_set(%0/%i0))][attrib_set(%0/FORM)];@switch [hasattrval(%0,FORM`SITE)]=1,{@trig #3745/TRIG`NOTIFY_SITE_USAGE=%0,%1,%q<tdb>,[sql(select name from territory_site where id=[xget(%0,FORM`SITE)])],[sql(select open from territory_site where id=[xget(%0,FORM`SITE)])]};@pemit %0=[ansi(hw,OSS:)]%bYour patrol has been recorded successfully. Thanks for using the automation tools.

@@ Passed - %0 Player DBREF, %1 - Time it happened, %2 - Territory dbref of the site, %3 - Name of the Site, %4 - open or closed (1 for open, 0 for closed)
&TRIG`NOTIFY_SITE_USAGE %we=[setq(a,sql(select by_dbref from territory_action where DATE_ADD(dt_time\,INTERVAL 7 DAY) > now() and type=1 and territory_dbref=%2,:))][setq(B,sql(select id from retainer_actions where target_dbref=%2 and type=1 and dt_ends>now(),:))][setq(C,sql(select id from player_actions where type=4 and dt_time_ends>now(),:))];@switch [cor(words(%qB,:),words(%qC,:))]=0,{@mail [switch(%4,1,[iter(%qA,[if(setr(temp,sql(select player_dbref from retainers where name='[name(#%i0)]')),#%q<temp>,#%i0)],:,%b)]%b[if(setr(temp,sql(select kingpin_dbref from territory where dbref=%2)),#%q<temp>)],0,[iter(%qA,[if(setr(temp,sql(select player_dbref from retainers where name='[name(#%i0)]')),#%q<temp>,#%i0)],:,%b)])]=Site Notice/%ROn [timefmt($Y-$m-$d $H:$M:$S,%1)] [name(%0)] used %3 to perform a task.}

@@ Helper Functions
@@ passed - %0 player dbref, %1 - dt amount remaining, %2 - Description
&FN`INSERT_PATROL_DT_RECORD %we=[sql(INSERT INTO Patrols (Player_DBREF\,Amount\,Patrols.Description) VALUES ([sqlescape(trim(%0,#))]\,[sqlescape(%1)]\,'%2'))]

&FN`CAN_ACCESS %we=[setq(q,sql(select territory_dbref from territory_site where id=[extract(%1,2,1,~)]))][cor(extract(%1,5,1,~),strmatch(trim(%0,#),sql(select kingpin_dbref from territory where dbref=%qQ)),get_merit(%0,Status [sql(select influence from territory_site where id=[extract(%1,2,1,~)])]),has_merit(%0,Barfly))]

&FN`OSS.CHECK.RETAINER.RET_COVERUP %we=[setq(Z,[extract(%0,8,1,~)]|[extract(%0,9,1,~)]|[extract(%0,10,1,~)])][setq(X,[extract(%0,5,1,~)] [extract(%0,6,1,~)] [extract(%0,7,1,~)])][switch([match(%qX,Streetwise)][match(%qX,Larceny)][match(%qX,Subterfuge)],1*,[setq(B,[extract(%0,4,1,~)]:1)][attrib_set(%1/FORM`SKILL,Streetwise)],01*,[setq(B,[extract(%0,4,1,~)]:1)][attrib_set(%1/FORM`SKILL,Larceny)],001,[setq(B,[extract(%0,4,1,~)]:1)][attrib_set(%1/FORM`SKILL,Subterfuge)],[setq(B,[extract(%0,4,1,~)]:0)])][if([setr(temp,[match(%qZ,Dominate*,|)])],[setq(C,after(extract(%qZ,%q<temp>,1,|),:))],[setq(C,0)])][if([setr(temp,[match(%qZ,Nightmare*,|)])],[setq(D,after(extract(%qZ,%q<temp>,1,|),:))],[setq(d,0)])][if([setr(temp,[match(%qZ,Obfuscate*,|)])],[setq(E,after(extract(%qZ,%q<temp>,1,|),:))],[setq(E,0)])][attrib_set(%1/FORM`RET.SKILL,%qB)][attrib_set(%1/FORM`DISC.DOM,%qC)][attrib_set(%1/FORM`DISC.NIGHT,%qD)][attrib_set(%1/FORM`DISC.OBF,%qE)]

&FN`OSS.CHECK.RETAINER.RET_PATROL %we=[if(match([extract(%0,5,1,~)] [extract(%0,6,1,~)] [extract(%0,7,1,~)],Streetwise),[setq(B,[extract(%0,4,1,~)]:1)],[setq(B,[extract(%0,4,1,~)]:0)])][attrib_set(%1/FORM`RET.SKILL,%qB)]

&FN`CHECK_TAG_SKILL %we=[setq(A,xget(%1,FORM`SKILL))][setq(B,sql(select skill_1\, skill_2\, skill_3 from retainers where id=[sqlescape(%0)]))][if(match(%qB,%qA),1,0)]

@@ Retainers Section
&CMD`OSS_PLAYER_AUTOMATION_LIST_RETAINERS %we=$+retainers:@assert [setr(a,sql(u(%vt/FN`RETAINERS.SQL_PLAYER_GET_RETAINERS,trim(%#,#)),|,~))]={@pemit %#=[ansi(hw,OSS:)]%bIt doesn't look like you have any retainers.};@pemit %#=[u(#3987/ST_LIST_RETAINERS,%qA)]

&CMD`OSS_PLAYER_AUTOMATION_LIST_RETAINERS_STAFF %we=$+retainers *:@assert [orflags(%#,Wr)];@assert [pmatch(%0)]={@pemit %#=Doesn't match a player};@assert [setr(a,sql(u(%vt/FN`RETAINERS.SQL_PLAYER_GET_RETAINERS,trim(pmatch(%0),#)),|,~))]={@pemit %#=[ansi(hw,OSS:)]%bIt doesn't look like they have any retainers.};@pemit %#=[u(#3987/ST_LIST_RETAINERS,%qA)]

&CMD`OSS_PLAYER_AUTOMATION_LIST_DEF_ACTIONS %we=$+retactions:@assert [setr(a,sql(u(%vt/FN`RETAINERS.SQL_PLAYER_GET_RETAINERS,trim(%#,#)),|,~))]={@pemit %#=[ansi(hw,OSS:)]%bIt doesn't look like you have any retainers.};@pemit %#=[u(#3987/ST_LIST_RETAINERS_DEFACT,%qA)]

&CMD`OSS_PLAYER_AUTOMATION_LIST_DEF_ACTIONS_STAFF %we=$+retactions *:@assert [orflags(%#,Wr)];@assert [pmatch(%0)]={@pemit %#=Doesn't match a player};@assert [setr(a,sql(u(%vt/FN`RETAINERS.SQL_PLAYER_GET_RETAINERS,trim(pmatch(%0),#)),|,~))]={@pemit %#=[ansi(hw,OSS:)]%bIt doesn't look like they have any retainers.};@pemit %#=[u(#3987/ST_LIST_RETAINERS_DEFACT,%qA)]

@@ Patrol Upkeep Trigger
&FN`CHECK_PATROL_DT #1773=[setq(A,[sql(SELECT * FROM Patrols,:,~)])][iter(%qA,[u(#2930/FN`INSERT,#[extract(%i0,2,1,~)],1,[extract(%i0,4,1,~)])][if(lte(dec(extract(%i0,3,1,~)),0),[sql(DELETE FROM Patrols WHERE id=[extract(%i0,1,1,~)])],[sql(UPDATE Patrols SET amount=[dec(extract(%i0,3,1,~))] WHERE id=[extract(%i0,1,1,~)])])],:)]

@@ Patrols command
&CMD`PLAYERS_OSS_PATROLS %we=$+patrols:@assert [words(setr(a,sql(SELECT * FROM Patrols WHERE Player_DBREF=[trim(%#,#)],:,~)),:)]=@pemit %#=[ansi(hw,Notice:)]%bYou don't have any patrols in the system.;@pemit %#=[header(Patrols)]%R[center(Days Remaining,39)][center(Territory,39)]%R%b[repeat(-,76)]%R[iter(%qA,[center(extract(%i0,3,1,~),39)][center(rest(extract(%i0,4,1,~)),39)],:,%R)]%R[footer()]


&VT %we=#1246
&WS %we=#1770
&WD %we=#2930
&WE %we=#3745

